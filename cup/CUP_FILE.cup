/*************/
/* IMPORT(S) */
/*************/
import java_cup.runtime.*;
import ast.*;
import java.util.ArrayList;

parser code 
{:
	public Lexer lexer;

	public Parser(Lexer lexer)
	{
		super(lexer);
		this.lexer = lexer;
	}
	public void report_error(String message, Object info)
	{
		System.out.print("ERROR >> ");		
		System.out.print("[");
		System.out.print(lexer.getLine());
		System.out.print(":");
		System.out.print(lexer.getTokenStartPosition());
		System.out.print("] ");		
		System.exit(0);
	}
:}

/************************/
/* CUSTOMIZE CUP PARSER */
/************************/
scan with
{:
	Symbol s;
	s = lexer.next_token();
	//System.out.print(s.sym);
	System.out.print("[");
	System.out.print(lexer.getLine());
	System.out.print(":");
	System.out.print(lexer.getTokenStartPosition());
	System.out.print("] ");
	System.out.print(TokenNames.terminalNames[s.sym]);
	if (s.value != null)
	{
		System.out.print("( ");
		System.out.print(s.value);
		System.out.print(" )");
	}
	System.out.print("\n");
	return s; 
:};

/*************/
/* TERMINALS */
/*************/
terminal IF;
terminal EQ;
terminal DOT;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal WHILE;
terminal DIVIDE;
terminal LPAREN;
terminal RPAREN;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal SEMICOLON;
terminal ASSIGN;

terminal GT;
terminal LT;
terminal COMMA;

terminal ARRAY;
terminal CLASS;
terminal RETURN;
terminal ELSE;
terminal NEW;
terminal EXTENDS;
terminal NIL;
terminal TYPE_INT;
terminal TYPE_STRING;
terminal VOID;

/*************/
/* TERMINALS */
/*************/
terminal Integer INT;
terminal String ID;
terminal String STRING;

/*****************/
/* NON TERMINALS */
/*****************/
non terminal AstExp exp;
non terminal AstExpNew newExp;
non terminal AstExp callExp;

non terminal AstVar var;
non terminal AstStmt stmt;
non terminal AstStmtList stmtList;
non terminal AstProgram program;
non terminal AstType type;
non terminal AstTypedefArray arrayTypedef;


non terminal AstParam param;
non terminal AstParamList paramList;
non terminal AstDec dec;
non terminal AstDec cField;
non terminal AstDecList cFieldList;
non terminal AstVarDec varDec;
non terminal AstFuncDec funcDec;
non terminal AstClassDec classDec;

non terminal ArrayList<AstExp> args;


/***********************/
/* OPERATOR PRECEDENCE */
/***********************/
precedence left EQ;
precedence left LT;
precedence left GT;
precedence left PLUS;
precedence left MINUS;
precedence left TIMES;
precedence left DIVIDE;
precedence left DOT;
/************************/
/* START WITH: stmtList */
/************************/
start with program;	//stmtList;

/********************/
/* DERIVATION RULES */
/********************/


stmtList	::=		stmt:s	stmtList:l		{: RESULT = new AstStmtList(s,l);    :}
					| stmt:s				{: RESULT = new AstStmtList(s,null); :}
					;

var			::=		ID:name								{: RESULT = new AstVarSimple(name);       :}
					| var:v DOT ID:fieldName			{: RESULT = new AstVarField(v,fieldName); :}
					| var:v LBRACK exp:e RBRACK			{: RESULT = new AstVarSubscript(v,e);     :}
					;
					
stmt 		::= 	varDec:v                             {: RESULT = v; :}
    				| var:v ASSIGN exp:e SEMICOLON               {: RESULT = new AstStmtAssign(v,e); :}
					| var:v ASSIGN newExp:e SEMICOLON             {: RESULT = new AstStmtAssign(v,e); :}
					| RETURN exp:e SEMICOLON             {: RESULT = new AstStmtReturn(e); :}
					| RETURN SEMICOLON               	 {: RESULT = new AstStmtReturn(null); :}
					| IF LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE {: RESULT = new AstStmtIf(cond, body, null); :}

					| IF LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE 
					ELSE LBRACE stmtList:elseBody RBRACE          			{: RESULT = new AstStmtIf(cond, body, elseBody); :}

					| WHILE LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE {: RESULT = new AstStmtWhile(cond, body); :}
					| callExp:c SEMICOLON                      {: RESULT = c; :}
					;

exp 		::= 	INT:i 						        {: RESULT = new AstExpInt(i); :}
					| MINUS INT:i  						{: RESULT = new AstExpInt(-i); :}
					| STRING:s 							{: RESULT = new AstExpString(s); :}
					| NIL								{: RESULT = new AstExpNil(); :}
					| var:v								{: RESULT = new AstExpVar(v); :}
					| LPAREN exp:e RPAREN				{: RESULT = e; :}
					| exp:e1 PLUS  exp:e2				{: RESULT = new AstExpBinop(e1, e2, 0); :}
					| exp:e1 MINUS exp:e2				{: RESULT = new AstExpBinop(e1, e2, 1); :}
					| exp:e1 TIMES exp:e2				{: RESULT = new AstExpBinop(e1, e2, 2); :}
					| exp:e1 DIVIDE exp:e2				{: RESULT = new AstExpBinop(e1, e2, 3); :}
					| exp:e1 LT    exp:e2				{: RESULT = new AstExpBinop(e1, e2, 4); :}
					| exp:e1 GT    exp:e2				{: RESULT = new AstExpBinop(e1, e2, 5); :}
					| exp:e1 EQ    exp:e2				{: RESULT = new AstExpBinop(e1, e2, 6); :}
					| callExp:c							{: RESULT = c; :}
					;

param 		::= 	type:t ID:name 		{: RESULT = new AstParam(t, name); :}
    				;

paramList 	::=		param:p COMMA paramList:l      {: RESULT = new AstParamList(p, l); :}
					| param:p   			{: RESULT = new AstParamList(p, null); :}
    				;

program 	::= 	dec:d								{: RESULT = new AstProgram(new AstDecList(d, null)); :}
          			| dec:d program:p					{: RESULT = new AstProgram(new AstDecList(d, p.decs)); :}
          			;
					
dec 		::= 	varDec:v          {: RESULT = v; :}
      				| funcDec:f         {: RESULT = f; :}
      				| classDec:c        {: RESULT = c; :}
      				| arrayTypedef:a    {: RESULT = a; :}
      				;

type 		::= 	TYPE_INT                                {: RESULT =  new AstTypeInt(); :}
					| TYPE_STRING 							{: RESULT =  new AstTypeString(); :}
					| VOID 									{: RESULT =  new AstTypeVoid(); :}
					| ID: i									{: RESULT =  new AstTypeID(i); :}
					;

varDec 		::= 	type:t ID:name SEMICOLON        				{: RESULT =  new AstVarDec(t,name, null); :}
					| type:t ID:name ASSIGN exp:e SEMICOLON 		{: RESULT =  new AstVarDec(t,name,e); :}
					| type:t ID:name ASSIGN newExp:e SEMICOLON		{: RESULT =  new AstVarDec(t,name,e); :}
					;

funcDec 	::= 	type:t ID:name LPAREN RPAREN LBRACE stmtList:l RBRACE 					{: RESULT =  new AstFuncDec(t,name,null,l); :}
					| type:t ID:name LPAREN paramList:pl RPAREN LBRACE stmtList:l RBRACE  {: RESULT =  new AstFuncDec(t,name,pl,l); :}
					;

classDec 	::= 	CLASS ID:name LBRACE cFieldList:l RBRACE						{: RESULT =  new AstClassDec(name,null, l); :}
					| CLASS ID:name1 EXTENDS ID:name2 LBRACE cFieldList:l RBRACE			{: RESULT =  new AstClassDec(name1,name2, l); :}
					;

cField 		::= 	varDec:v 		{: RESULT = v; :}
					| funcDec:f 	{: RESULT = f; :}
					;

cFieldList ::=		cField:c cFieldList:l {: RESULT = new AstDecList(c, l); :}
					| cField:c {: RESULT = new AstDecList(c, null); :}
    				;

arrayTypedef ::=	ARRAY ID:name EQ type:t LBRACK RBRACK SEMICOLON {: RESULT = new AstTypedefArray(name, t); :}
    				;

newExp 	    ::=		NEW type:t		{: RESULT = new AstExpNew(t, null); :}
    				| NEW type:t LBRACK exp:e RBRACK {: RESULT = new AstExpNew(t, e); :}
    				;

callExp		::=		ID:id LPAREN args:a RPAREN {: RESULT = new AstExpCall(id, a); :}
    				| var:v DOT ID:id LPAREN args:a RPAREN {: RESULT = new AstExpMethodCall(v, id, a); :}
    				;

args 		::=		/* empty */ {: RESULT = new ArrayList<AstExp>(); :}
    				| exp:e
						{: 
						ArrayList<AstExp> list = new ArrayList<>();
						list.add(e);
						RESULT = list;
						:}
    				| args:list COMMA exp:e
						{: 
						list.add(e);
						RESULT = list;
						:}
					;
